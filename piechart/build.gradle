apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven'
apply plugin: 'signing'

android {

    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode libVersionCode.toInteger()
        versionName libVersion
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    implementation 'androidx.annotation:annotation:1.0.2'
}

dokka {
    moduleName = libDescription
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task dokkaJar(type: Jar, dependsOn: project.dokka) {
    classifier = 'javadoc'
    from dokka
}

artifacts {
    archives sourcesJar
    archives dokkaJar
}

if (hasProperty('signing.keyId')) {
    signing {
        sign configurations.archives
    }
}

uploadArchives {
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        pom.groupId = "$groupId"
        pom.artifactId = "$artifactIdPrefix-piechart"
        pom.version = "$libVersion"

        pom.project {

            name libName
            packaging 'jar'
            description libDescription
            url libUrl

            scm {
                connection 'scm:git:https://github.com/sczerwinski/android-charts.git'
                developerConnection 'scm:git:https://github.com/sczerwinski/android-charts.git'
                url 'https://github.com/sczerwinski/android-charts'
            }

            licenses {
                license {
                    name 'Apache License 2.0'
                    url 'https://www.apache.org/licenses/LICENSE-2.0'
                }
            }

            developers {
                developer {
                    id 'sczerwinski'
                    name 'Slawomir Czerwinski'
                    email 'slawomir@czerwinski.it'
                }
            }
        }

        if (System.getenv('SONATYPE_USERNAME') != null) {

            println("Uploading to Sonatype")

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(
                        userName: System.getenv('SONATYPE_USERNAME'),
                        password: System.getenv('SONATYPE_PASSWORD')
                )
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(
                        userName: System.getenv('SONATYPE_USERNAME'),
                        password: System.getenv('SONATYPE_PASSWORD')
                )
            }

        } else if (project.hasProperty('ossrhUsername')) {

            println("Uploading to Sonatype")

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

        } else {

            println("Uploading to local repository")

            repository(url: "file://${buildDir}/release/$libVersion")

        }
    }
}
